'***************************************************************
'Program 36237
'
'Description:
' Controller Configuration Parameters
'
'For Use with:
' Controller: DMC-4050(TRES8,MO)-C012-I200-I200-D3040(HALLF)-D3020(HALLF)
'    BN-39599
'Hardware Configuration:
'A: X Axis - LM7 Gantry Main Axis   SN: 1619455
'B: Y Axis - LM7 Across Gantry   SN: 1619450
'C: Z Axis - LM7 Vertical Axis   SN: 1619563
'D: T Axis - RTS-100-4Mcts/rev   SN: 1619456
'E: X Axis Slave - LM7 Gantry Slave  SN: 1619456
'
'Description:  Homing Routine for all axes
'
'For: Boston Scientific System 35537 (SN XXXXXXX)
'
'
'For Controller:
'DMC-4050
'
'Hardware Configuration:
'A: X Axis - LM7 Gantry Main Axis   SN: 1619455
'B: Y Axis - LM7 Across Gantry   SN: 1619450
'C: Z Axis - LM7 Vertical Axis   SN: 1619563
'D: T Axis - RTS-100-4Mcts/rev   SN: 1619942
'E: X Axis Slave - LM7 Gantry Slave  SN: 1619456
'
'
'
'---------------------------------------
'REVISIONS
'
'Rev 0 | 05/04/2023 | JG | Copied from 25908 rev A, combined with #CFG
'
'
'***************************************************************
'
'
'
'
'
#HomeAll
JS#CONFIG
XQ#HomeA,1
XQ#HomeB,2
XQ#HomeC,3
XQ#HomeD,4
EN
'
'
'
'
'
'
'
'
'
'
'
'
'
'
'
#CONFIG
CTSPMMA=3200
CTSPMMB=3200
CTSPMMC=3200
CTSPDGD=4000000/360
EN
'
'
'
'
#HomeA
'Save Current Settings
ACATEMP=_ACA
DCATEMP=_DCA
SPATEMP=_SPA
'
'
'SETUP
ACA=300*CTSPMMA
DCA=300*CTSPMMA
SPA=50*CTSPMMA
HVA=0.5*CTSPMMA
LDA=0
'
'INDEX OFFSET (MM)
OFFSETA=3.92
'
'GANTRY OFFSET (CTS)
'ENCODER CTS BETWEEN INDEX PULSES
'GOFFSET=1424
'GOFFSET=3262-600
GOFFSET=2662
'
'START OF MOTION
SHAE
'
'IF NOT IN NEG LIM
IF(_LRA = 1)
  IPA=-320*CTSPMMA
  AMA
ENDIF
'
'ALREADY IN NEGATIVE END OF TRAVEL
FEA
BGA
AMA
'
SPA=0.5*CTSPMMA
DCA=500*CTSPMMA
FEA
BGA
AMA
'
#TIMRST
TIMR=0
#WTSETL
TIMR=TIMR+10
WT10
IF((_TEA<>0) | (_TEA<>0))
  JP#TIMRST
ELSE
  IF(TIMR<900)
    JP#WTSETL
  ELSE
    JP#TIMRDON
  ENDIF
ENDIF
'
#TIMRDON
'
DPA=0
DPE=0
'
SPA=1*CTSPMMA
'LATCH INDEX ON A, E
ALTATE
'MOVE MORE THAN ONE ENCODER REV
IP18000
AMA
'
IF(_ALA | _ALE)
 'DID NOT LATCH AN INDEX PULSE
 MG"HOMING ERROR"
 SPA=20*CTSPMMA
 IPA=-100*CTSPMMA
ENDIF
'
SPA=20*CTSPMMA
PAA=_RLA
BGA
AMA
'
MG"GANTRY OFFSET: ", (_RLE - _RLA) - GOFFSET
'
'
MG(_RLE-_RLA),_RLE,_RLA
IF((_RLE-_RLA) <> GOFFSET)
  MG"GANTRY OFFSET: ", (_RLE - _RLA) - GOFFSET
  SPE = 3200
  MG"IPE=",(_RLE - _RLA) - GOFFSET
  IPE = (_RLE - _RLA) - GOFFSET
  AME
  MG"GANTRY ALIGNED"
ENDIF
'
'
'
'
WT500
DPA=OFFSETA*CTSPMMA
PAA=0
'
BGA
AMA
'
'RESET AC/DC/SP
ACA=ACATEMP
DCA=DCATEMP
SPA=SPATEMP
'
'
MG"A HOME"
'
EN
'END HomeA
'
'
'
'
'
'
#HomeB
'Save Current Settings
ACBTEMP=_ACB
DCBTEMP=_DCB
SPBTEMP=_SPB
'
'
'SETUP
ACB=300*CTSPMMB
DCB=300*CTSPMMB
SPB=20*CTSPMMB
HVB=0.5*CTSPMMB
LDB=0
'
'INDEX OFFSET (MM)
OFFSETB=2.80
'
'START OF MOTION
SHB
'
'IF NOT IN NEG LIM
IF(_LRB = 1)
  IPB=-320*CTSPMMB
  AMB
ENDIF
'
'ALREADY IN NEGATIVE END OF TRAVEL
FEB
BGB
AMB
'
SPB=0.5*CTSPMMB
DCB=500*CTSPMMB
FEB
BGB
AMB
'
JGB=1*CTSPMMB
FIB
BGB
AMB
'
WT500
DPB=OFFSETB*CTSPMMB
PAB=0
SPB=20*CTSPMMB
BGB
AMB
'
'RESET AC/DC/SP
ACB=ACBTEMP
DCB=DCBTEMP
SPB=SPBTEMP
'
'
MG"B HOME"
'
'
EN
'END HomeB
'
'
'
'
'
'
#HomeC
'Save Current Settings
ACCTEMP=_ACC
DCCTEMP=_DCC
SPCTEMP=_SPC
'
'
'SETUP
ACC=300*CTSPMMC
DCC=300*CTSPMMC
SPC=20*CTSPMMC
HVC=0.5*CTSPMMC
LDC=0
'
'INDEX OFFSET (MM)
OFFSETC=2.32
'
'START OF MOTION
SHC
'
'IF NOT IN NEG LIM
IF(_LRC = 1)
  IPC=-320*CTSPMMC
  AMC
ENDIF
'
'ALREADY IN NEGATIVE END OF TRAVEL
FEC
BGC
AMC
'
SPC=0.5*CTSPMMC
DCC=500*CTSPMMC
FEC
BGC
AMC
'
JGC=1*CTSPMMC
FIC
BGC
AMC
'
WT500
DPC=OFFSETC*CTSPMMC
PAC=0
SPC=20*CTSPMMC
BGC
AMC
'
'RESET AC/DC/SP
ACC=ACCTEMP
DCC=DCCTEMP
SPC=SPCTEMP
'
'
MG"C HOME"
'
'
EN
'END HomeC
'
'
'
'
#HomeD
'Save Current Settings
ACDTEMP=_ACD
DCDTEMP=_DCD
SPDTEMP=_SPD
'
'
'SETUP
ACD=300*CTSPDGD
DCD=300*CTSPDGD
SPD=20*CTSPDGD
HVD=0.5*CTSPDGD
LDD=3
'
'INDEX OFFSET (DEG)
OFFSETD=0
'
'START OF MOTION
SHD
'
IPD=-5*CTSPDGD
AMD
'
JGD=20*CTSPDGD
FID
BGD
AMD
'
DPD=OFFSETD*CTSPDGD
PAD=0
BGD
AMD
'
'
'RESET AC/DC/SP
ACD=ACDTEMP
DCD=DCDTEMP
SPD=SPDTEMP
'
'
MG"D HOME"
EN
'END #HomeD
'
#POSERR
 IF(_MOA)
   IF(_MOE=0)
     MOE
     'MG"POSERR SHUTDOWN E"
   ENDIF
 ENDIF
'
 IF(_MOE)
   IF(_MOA=0)
     ERATEMP=_ERA
     ERA=1
     STA
     AMA
     MOA
     ERA=ERATEMP
     MG"POSERR SHUTDOWN A"
   ENDIF
 ENDIF
RE
'
'
#CFG
MO;'Motors must be off to set AG
TM 1000.0000
AG 2, 2, 2, 1, 2
OF 0.0000, 0.0000, 0.0000, 0.0000, 0.0000
AU 0.0, 0.0, 0.0, 0.0, 0.0
LD 0, 0, 0, 3, 0
OE 1, 1, 1, 1, 1
CN 1.0000, -1.0000, -1.0000
MT 1.0, 1.0, 1.0, 1.0, 1.0
BW 0, 0, 0, 0, 0
FL 2147483647, 2147483647, 2147483647, 2147483647, 2147483647
BL -2147483648, -2147483648, -2147483648, -2147483648, -2147483648
'
'Tuning Setup
'KP 50.00,   50.00,    50.00,     4.00,    50.00
'KI 1.0000,   1.0000,   1.0000,   1.0000,   1.0000
'KD 250.00, 250.00,   250.00,    10.00,   250.0
'
'Tune With Pole
KP 50.00,   50.00,    50.00,     0.8,    50.00
KI 1.0000,   1.0000,   1.0000,   0.1,   1.0000
KD 250.00, 250.00,   250.00,    30.00,   250.0
PL 0.0000, 0.0000, 0.0000, 0.8000, 0.0000
'
'
FA 0.0000, 0.0000, 0.0000, 0.0000, 0.0000
FV 0, 0, 0, 0, 0
IL 9.9982, 9.9982, 9.9982, 9.9982, 9.9982
'PL 0.0000, 0.0000, 0.0000, 0.0000, 0.0000
NB 0.5, 0.5, 0.5, 0.5, 0.5
NF 0, 0, 0, 0, 0
'
'Limits
TK 9.9, 9.9, 9.9, 9.9, 9.9
TL 7.0, 7.0, 7.0, 5.6, 7.0
ER 3200, 3200, 3200, 11111, 3200
'
'Motion Settings
AC 320000, 320000, 320000, 6666666, 320000
DC 320000, 320000, 320000, 6666666, 320000
SP 32000, 32000, 32000, 1111111, 32000
SD 1920000, 1920000, 1920000, 256000, 1920000
HV 1600, 1600, 1600, 5000, 1600
LC 0, 0, 0, 0, 0
YA 2, 2, 2, 2, 2
BM 2000.0000, 2000.0000, 2000.0000, 2000.0000, 2000.0000
BX< 200
BI 0, 0, 0, 0, 0
'
'GANTRY
GR 0, 0, 0, 0, 1
GM 0, 0, 0, 0, 1
GAE=CA
'
MG"#CFG Complete."
'
EN