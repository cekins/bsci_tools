'#######################################
'Program 25908
'
'Description:  Homing Routine for all axes
'
'For: Boston Scientific System
'
'
'For Controller:
'DMC-4050
'
'Hardware Configuration:
'A: X Axis - LM7 Gantry Main Axis
'B: Y Axis - LM7 Across Gantry
'C: Z Axis - LM7 Vertical Axis
'D: T Axis - RTS-100-4Mcts/rev
'E: X Axis Slave - LM7 Gantry Slave Axis
'
'
'
'---------------------------------------
'REVISIONS
'
'Rev A | 03/03/2015 | JG | Added Program #
'Rev B | 09/24/2015 | KW | Adjusted to be compatible with impedance tank
'
'---------------------------------------
'#######################################
'
'
'
'
'
#Hm_wTnk
JS#CONFIG
XQ#HomeA,1
XQ#HomeB,2
XQ#HomeC,3
XQ#HomeD,4
EN
'
'
'
'
#CONFIG
CTSPMMA=3200
CTSPMMB=3200
CTSPMMC=3200
CTSPDGD=4000000/360
cFLAG = 0
EN
'
#waitC
WT 5000
JS #waitC, cFLAG <> 1
EN
'
'
#HomeA
'Save Current Settings
ACATEMP=_ACA
DCATEMP=_DCA
SPATEMP=_SPA
'
'
'SETUP
ACA=300*CTSPMMA
DCA=300*CTSPMMA
SPA=20*CTSPMMA
HVA=0.5*CTSPMMA
LDA=0
'
'INDEX OFFSET (MM)
OFFSETA=0.0744
'
'GANTRY OFFSET (CTS)
'ENCODER CTS BETWEEN INDEX PULSES
GOFFSET=3231
'
'START OF MOTION
SHAE
'
JS #waitC
'
'IF NOT IN NEG LIM
IF(_LRA = 1)
  IPA=-320*CTSPMMA
  AMA
ENDIF
'
'ALREADY IN NEGATIVE END OF TRAVEL
FEA
BGA
AMA
'
SPA=0.5*CTSPMMA
DCA=500*CTSPMMA
FEA
BGA
AMA
'
#TIMRST
TIMR=0
#WTSETL
TIMR=TIMR+10
WT10
IF((_TEA<>0) | (_TEA<>0))
  JP#TIMRST
ELSE
  IF(TIMR<900)
    JP#WTSETL
  ELSE
    JP#TIMRDON
  ENDIF
ENDIF
'
#TIMRDON
'
DPA=0
DPE=0
'
SPA=1*CTSPMMA
'LATCH INDEX ON A, E
ALTATE
'MOVE MORE THAN ONE ENCODER REV
IP18000
AMA
'
IF(_ALA | _ALE)
 'DID NOT LATCH AN INDEX PULSE
 MG"HOMING ERROR"
 SPA=20*CTSPMMA
 IPA=-100*CTSPMMA
ENDIF
'
SPA=20*CTSPMMA
PAA=_RLA
BGA
AMA
'
'MG(_RLE-_RLA),_RLE,_RLA
IF((_RLE-_RLA) <> GOFFSET)
  MG"GANTRY OFFSET: ", (_RLE - _RLA) - GOFFSET
  SPE = 3200
  IPE = (_RLE - _RLA) - GOFFSET
  AME
  MG"GANTRY ALIGNED"
ENDIF
'
'
'
'
WT500
DPA=OFFSETA*CTSPMMA
PAA=0
'
BGA
AMA
'
'RESET AC/DC/SP
ACA=ACATEMP
DCA=DCATEMP
SPA=SPATEMP
'
'
MG"A HOME"
'
EN
'END HomeA
'
'
'
'
'
'
#HomeB
'Save Current Settings
ACBTEMP=_ACB
DCBTEMP=_DCB
SPBTEMP=_SPB
'
'
'SETUP
ACB=300*CTSPMMB
DCB=300*CTSPMMB
SPB=20*CTSPMMB
HVB=0.5*CTSPMMB
LDB=0
'
'INDEX OFFSET (MM)
OFFSETB=4.136
'
'START OF MOTION
SHB
'
JS #waitC
'
'IF NOT IN NEG LIM
IF(_LRB = 1)
  IPB=-320*CTSPMMB
  AMB
ENDIF
'
'ALREADY IN NEGATIVE END OF TRAVEL
FEB
BGB
AMB
'
SPB=0.5*CTSPMMB
DCB=500*CTSPMMB
FEB
BGB
AMB
'
JGB=1*CTSPMMB
FIB
BGB
AMB
'
WT500
DPB=OFFSETB*CTSPMMB
PAB=0
SPB=20*CTSPMMB
BGB
AMB
'
'RESET AC/DC/SP
ACB=ACBTEMP
DCB=DCBTEMP
SPB=SPBTEMP
'
'
MG"B HOME"
'
'
EN
'END HomeB
'
'
'
'
'
'
#HomeC
'Save Current Settings
ACCTEMP=_ACC
DCCTEMP=_DCC
SPCTEMP=_SPC
'
'
'SETUP
ACC=300*CTSPMMC
DCC=300*CTSPMMC
SPC=20*CTSPMMC
HVC=0.5*CTSPMMC
LDC=0
'
'INDEX OFFSET (MM)
OFFSETC=1.8556
'
'START OF MOTION
SHC
'
'IF NOT IN NEG LIM
IF(_LRC = 1)
  IPC=-320*CTSPMMC
  AMC
ENDIF
'
'
'ALREADY IN NEGATIVE END OF TRAVEL
FEC
BGC
AMC
'
SPC=0.5*CTSPMMC
DCC=500*CTSPMMC
FEC
BGC
AMC
'
JGC=1*CTSPMMC
FIC
BGC
AMC
'
WT500
DPC=OFFSETC*CTSPMMC
PAC=-300*3200
SPC=20*CTSPMMC
BGC
AMC
'
'RESET AC/DC/SP
ACC=ACCTEMP
DCC=DCCTEMP
SPC=SPCTEMP
'
cFLAG = 1
'
MG"C HOME"
'
'
EN
'END HomeC
'
'
'
'
#HomeD
'Save Current Settings
ACDTEMP=_ACD
DCDTEMP=_DCD
SPDTEMP=_SPD
'
'
'SETUP
ACD=300*CTSPDGD
DCD=300*CTSPDGD
SPD=20*CTSPDGD
HVD=0.5*CTSPDGD
LDD=3
'
'INDEX OFFSET (DEG)
OFFSETD=0
'
'START OF MOTION
SHD
'
JS #waitC
'
IPD=-5*CTSPDGD
AMD
'
JGD=20*CTSPDGD
FID
BGD
AMD
'
DPD=OFFSETD*CTSPDGD
PAD=0
BGD
AMD
'
'
'RESET AC/DC/SP
ACD=ACDTEMP
DCD=DCDTEMP
SPD=SPDTEMP
'
'
MG"D HOME"
EN
'END #HomeD
'
#POSERR
 IF(_MOA)
   IF(_MOE=0)
     MOE
     'MG"POSERR SHUTDOWN E"
   ENDIF
 ENDIF
'
 IF(_MOE)
   IF(_MOA=0)
     ERATEMP=_ERA
     ERA=1
     STA
     AMA
     MOA
     ERA=ERATEMP
     MG"POSERR SHUTDOWN A"
   ENDIF
 ENDIF
RE